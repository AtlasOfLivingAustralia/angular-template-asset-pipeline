buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "https://jaspersoft.jfrog.io/artifactory/third-party-ce-artifacts"}
        maven { url 'https://jitpack.io' }
    }

    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
    }

}

apply plugin:"eclipse"
apply plugin:"idea"
apply plugin:"java"
apply plugin:"groovy"
apply plugin:"maven-publish"

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "https://nexus.ala.org.au/content/groups/public/" }
    maven { url 'https://jitpack.io' }
}

group = 'com.craigburke.angular'
version = '2.5.0'

dependencies {
    implementation "com.bertramlabs.plugins:asset-pipeline-core:$assetPipelineVersion"
    implementation 'org.codehaus.groovy:groovy-all:3.0.11'
    implementation 'org.mozilla:rhino:1.7R4'
    implementation 'com.googlecode.htmlcompressor:htmlcompressor:1.5.2'

    testImplementation 'org.spockframework:spock-core:2.2-groovy-3.0'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/libs'
}

tasks.withType(Test) {
    useJUnitPlatform()
}


publishing {
    repositories {
        maven {
            name 'Nexus'
            url "https://nexus.ala.org.au/content/repositories/${project.version.endsWith('-SNAPSHOT') ? 'snapshots' : 'releases'}"
            credentials {
                username = System.getenv('TRAVIS_DEPLOY_USERNAME')
                password = System.getenv('TRAVIS_DEPLOY_PASSWORD')
            }
        }
    }

    publications {
        mavenJar(MavenPublication) {
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name 'angular-template-asset-pipeline'
                    description 'AngularJS Templates extension for the Asset Pipeline Library.'
                    url 'https://github.com/craigburke/angular-template-asset-pipeline'
                    scm {
                        url 'https://github.com/craigburke/angular-template-asset-pipeline'
                        connection 'scm:https://github.com/craigburke/angular-template-asset-pipeline.git'
                        developerConnection 'scm:https://github.com/craigburke/angular-template-asset-pipeline.git'
                    }
                    licenses {
                        license {
                            name 'The Apache Software License, Version 2.0'
                            url 'http://www.apache.org/license/LICENSE-2.0.txt'
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id 'craigburke'
                            name 'Craig Burke'
                            email 'craig@craigburke.com'
                        }
                    }
                }
            }

            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}